*September 05, 2005
*Doc. ID: 77819, S-51617, Rev. A
*File Name: Si7336ADP_PS.txt and Si7336ADP_PS.lib
*.SUBCKT Si7336ADP 4 1 2
.SUBCKT Si7336ADP D G S
*M1  3 1 2 2 NMOS W=10594402u L=0.25u
M1  3 G S S NMOS W=10594402u L=0.25u
*M2  2 1 2 4 PMOS W=10594402u L=0.25u
M2  S G S D PMOS W=10594402u L=0.25u
*R1  4 3     RTEMP 1.1E-3
R1  D 3     RTEMP 1.1E-3
*CGS 1 2     3100E-12
CGS G S     3100E-12
*DBD 2 4     DBD
DBD S D     DBD
*******************************************************************
.MODEL  NMOS       NMOS ( LEVEL  = 3               TOX    = 5E-8
+ RS     = 1E-3           RD     = 0               NSUB   = 3.2E17
+ KP     = 1.3E-5         UO     = 650
+ VMAX   = 0              XJ     = 5E-7            KAPPA  = 1E-5
+ ETA    = 1E-4           TPG    = 1
+ IS     = 0              LD     = 0
+ CGSO   = 0              CGDO   = 0               CGBO   = 0
+ NFS    = 0.8E12         DELTA  = 0.1)
*******************************************************************
.MODEL  PMOS       PMOS ( LEVEL  = 3               TOX    = 5E-8
+NSUB    = 2E16           TPG    = -1)
*******************************************************************
.MODEL DBD D (CJO=1800E-12 VJ=0.38 M=0.30
+RS=0.1 FC=0.5 IS=1E-12 TT=5E-8 N=1 BV=30.1)
*******************************************************************
.MODEL RTEMP RES (TC1=6E-3 TC2=5.5E-6)
*******************************************************************
.ENDS
